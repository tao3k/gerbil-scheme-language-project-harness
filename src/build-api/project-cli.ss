(export #t)

(import :gerbil/tools/env
        :std/cli/getopt
 :std/cli/multicall
  :gslph/src/build-api/project-build
  :gslph/src/testing/project-build
  :gslph/src/build-api/source-coverage)

(import :gslph/src/build-api/component-closure)

(def +package-root+ (current-directory))

(configure-project-build-root! +package-root+)
(gslph-source-coverage
 roots: ["src"]
 runtime-roots: ["src"])

(def (native-build-getopt . options)
  (append
   [(flag 'verbose "-V" "--verbose"
          help: "Make the build verbose")
    (flag 'debug "-g" "--debug"
          help: "Include debug information")
    (flag 'no-optimize "--O" "--no-optimize"
          help: "Disable Gerbil optimization")
    (flag 'optimized "-O" "--optimized"
          help: "Accept gxpkg optimized build mode")
    (flag 'release "-R" "--release"
          help: "Build optimized release executables")]
   options))

(def compile-getopt
  (native-build-getopt
   (flag 'binary "--binary"
         help: "Build the package-local development CLI executable under .bin")
   (flag 'full "--full"
         help: "Compile every library module instead of the CLI launcher")))

(def install-getopt
  (native-build-getopt
   (flag 'full "--full"
         help: "Compile every library module before installing the CLI launcher")))

(def test-file-getopt
  [(rest-arguments 'files
                   help: "Selected gxtest files")])

(def component-spec-getopt
  [(rest-arguments 'components
                   help: "Named component")])

(define-entry-point (compile (verbose #f)
                             (debug #f)
                             (no-optimize #f)
                             (optimized #f)
                             (release #f)
                             (binary #f)
                             (full #f))
  (help: "Compile the package"
   getopt: compile-getopt)
  (project-compile-target
   verbose debug no-optimize optimized release full binary))

(define-entry-point (spec)
  (help: "Show the build specification"
   getopt: [])
  (displayln (project-compile-spec #t #f #f)))

(define-entry-point (install (verbose #f)
                             (debug #f)
                             (no-optimize #f)
                             (optimized #f)
                             (release #f)
                             (full #f))
  (help: "Install optimized release gslph into $HOME/.local/bin"
   getopt: install-getopt)
  (project-install-target
   verbose debug no-optimize optimized release full))

(define-entry-point (clean)
  (help: "Clean package-local development build artifacts"
   getopt: [])
  (project-clean-target))

(define-entry-point (test)
  (help: "Run the default fast gxtest smoke gate"
   getopt: [])
  (project-test-target))

(define-entry-point (test-file . files)
  (help: "Run selected gxtest files"
   getopt: test-file-getopt)
  (project-test-file-target files))

(define-entry-point (test-full)
  (help: "Run every top-level gxtest file"
   getopt: [])
  (project-test-full-target))

(define-entry-point (component-spec . components)
  (help: "Write a checked component source-closure receipt"
   getopt: component-spec-getopt)
  (match components
    ([component] (write-gslph-component-receipt component))
    (else (error "component-spec requires exactly one component" components))))

(define-multicall-main)
